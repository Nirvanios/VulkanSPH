#version 460

#extension GL_EXT_debug_printf : enable

#define TO_INDEX(a, b, c)                                                                          \
  (uint(a                                                                                          \
        + simulationInfoSph.gridSizeXYZcountW.x                                                    \
            * (b + simulationInfoSph.gridSizeXYZcountW.y * c)))

#define VEC_TO_INDEX(a) (TO_INDEX(a.x, a.y, a.z))

float M_PI = 3.1415;

float defaultKernel(in vec4 position, in float positionNorm, in float supportRadius) {
  if (positionNorm >= 0.0f && positionNorm <= supportRadius)
    return (315 / (64 * M_PI * pow(supportRadius, 9)))
        * pow(pow(supportRadius, 2) - pow(positionNorm, 2), 3);
  else
    return 0.0f;
}

struct SimulationInfoSPH {
  ivec4 gridSizeXYZcountW;
  vec4 GridOrigin;
  vec4 gravityForce;
  float particleMass;
  float restDensity;
  float viscosityCoefficient;
  float gasStiffnessConstant;
  float heatConductivity;
  float heatCapacity;
  float timeStep;
  float supportRadius;
  float tensionThreshold;
  float tensionCoefficient;
  uint particleCount;
};

struct GridInfoMC {
  ivec4 gridSize;
  vec4 gridOrigin;
  float cellSize;
  int detail;
  float threshold;
};

layout(push_constant) uniform MarchingCubesInfo {
  SimulationInfoSPH simulationInfoSph;
  GridInfoMC gridInfoMC;
};

struct ParticleRecord {
  vec4 position;
  vec4 velocity;
  vec4 previousVelocity;
  vec4 massDensityCenter;
  vec4 force;
  float massDensity;
  float pressure;
  float temperature;
  int gridID;
  float pressureForceLength;
  float surfaceArea;
  float weightingKernelFraction;
  float weight;
};

#define WALL_DAMPING 0.3f

struct KeyValue {
  int key;  //Particle ID
  int value;//Cell ID
};

struct CellInfo {
  uint tags;
  int indexes;
};

layout(std430, binding = 0) buffer positionBuffer { ParticleRecord particleRecords[]; };
layout(std430, binding = 1) buffer Grid { KeyValue grid[]; };
layout(std430, binding = 2) buffer Indexes { CellInfo cellInfos[]; };
layout(std430, binding = 3) buffer Colors { float colorField[]; };

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

float computeColor(float neighbourWeightedMass, uint neighbourID, vec4 positionDiff) {
  return (neighbourWeightedMass / particleRecords[neighbourID].massDensity)
      * defaultKernel(positionDiff, length(positionDiff), simulationInfoSph.supportRadius);
}

void main() {
  const uvec3 fieldSize = gridInfoMC.gridSize.xyz + 1;

  const uint myId = gl_GlobalInvocationID.x;
  ivec3 myId3Dmc = ivec3(myId % fieldSize.x, (myId % (fieldSize.x * fieldSize.y) / fieldSize.x),
                         myId / (fieldSize.x * fieldSize.y));
  ivec3 myId3Dsph = myId3Dmc / gridInfoMC.detail;
  myId3Dmc -= ivec3(1 * gridInfoMC.detail);
  myId3Dsph -= ivec3(1);

  float color = 0.0f;

  vec4 myPosition = vec4(myId3Dmc * gridInfoMC.cellSize, 0);

  for (int z = -1; z < 2; ++z) {
    for (int y = -1; y < 2; ++y) {
      for (int x = -1; x < 2; ++x) {
        ivec3 currentGridID3D = myId3Dsph + ivec3(x, y, z);
        uint currentGridID = VEC_TO_INDEX(currentGridID3D);

        if (all(lessThan(currentGridID3D, simulationInfoSph.gridSizeXYZcountW.xyz))
            && all(greaterThanEqual(currentGridID3D, ivec3(0)))) {
          int sortedID = cellInfos[currentGridID].indexes;

          if (sortedID == -1) { continue; }

          while (grid[sortedID].value == currentGridID
                 && sortedID < simulationInfoSph.particleCount) {

            const int neighbourID = grid[sortedID].key;
            if (particleRecords[neighbourID].weight > 0) {

              vec4 positionDiff = myPosition - particleRecords[neighbourID].position;

              if (length(positionDiff) < simulationInfoSph.supportRadius) {

                float neighbourWeightedMass =
                    simulationInfoSph.particleMass * particleRecords[neighbourID].weight;

                color += computeColor(neighbourWeightedMass, neighbourID, positionDiff);
              }
            }
            ++sortedID;
          }
        }
      }
    }
  }
  colorField[myId] = color;
}
