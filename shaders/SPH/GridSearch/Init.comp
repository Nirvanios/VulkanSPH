#version 460

layout(push_constant) uniform GridInfo {
  ivec4 gridSize;
  vec4 gridOrigin;
  float cellSize;
  uint particleCount;
};

struct KeyValue {
  int key;  //Particle ID
  int value;//Cell ID
};

layout(std430, binding = 0) buffer Grid { KeyValue grid[]; };

struct ParticleRecord {
  vec4 position;
  vec4 velocity;
  vec4 currentVelocity;
  float massDensity;
  float pressure;
  int gridID;
  float dummy;
};

layout(std430, binding = 1) buffer positionBuffer { ParticleRecord particleRecords[]; };

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

void main() {
  const uint myId = gl_GlobalInvocationID.x;
  if (myId < particleCount) {
    vec4 relativePosition = particleRecords[myId].position - gridOrigin;
    ivec3 gridIndex3D = ivec3(floor(relativePosition.xyz / cellSize));
    int gridIndexFlatten =
        gridIndex3D.x + gridSize.x * (gridIndex3D.y + gridSize.z * gridIndex3D.z);
    grid[myId].key = int(myId);
    grid[myId].value = gridIndexFlatten;
    particleRecords[myId].gridID = gridIndexFlatten;
  }
}
